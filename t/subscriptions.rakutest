#!/usr/bin/env raku

use Test;
use Test::Mock;

use lib 'lib';

use Nats::Subscriptions;
use Nats;
use Nats::Subscription;

use-ok "Nats::Subscriptions";

can-ok Nats::Subscriptions, 'new';
can-ok Nats::Subscriptions, 'subscriptions';

ok &subscribe, 'subscribe';
ok &message, 'message';
ok &subscriptions, 'subscriptions';

my @*SUBSCRIPTIONS;

subscribe -> $ {};
is @*SUBSCRIPTIONS.elems, 1, 'created subscriptions';
is @*SUBSCRIPTIONS.tail.signature.params.elems, 1, 'subscriptions function has one parameter';
is @*SUBSCRIPTIONS.tail.signature.params.head.type, Nats, 'subscriptions function has one parameter of type Nats';

subscribe -> $ {};
is @*SUBSCRIPTIONS.elems, 2, 'created subscriptions';
is @*SUBSCRIPTIONS.tail.signature.params.elems, 1, 'subscriptions function has one parameter';
is @*SUBSCRIPTIONS.tail.signature.params.head.type, Nats, 'subscriptions function has one parameter of type Nats';

my $*MESSAGE = rand;
is message, $*MESSAGE, 'message returns message';

my Bool $ran = False;
my $r1 = subscriptions {
    pass "subscriptions block has ran";
    $ran = True;
}
ok $ran, 'subscriptions block has ran';
isa-ok $r1, Nats::Subscriptions, 'subscriptions returns a Nats::Subscriptions';

my $queue = "my-queue";
my $max-messages = 10;

my $r2 = subscriptions -> {
    subscribe -> "bla"                          { pass "bla"         }
    subscribe -> "bla", "ble"                   { pass "bla.ble"     }
    subscribe -> "bla", "ble", "bli"            { pass "bla.ble.bli" }
    subscribe -> $                              { pass "*"           }
    subscribe -> $, $                           { pass "*.*"         }
    subscribe -> $, $, $                        { pass "*.*.*"       }
    subscribe -> "bla", $, "bli"                { pass "bla.*.bli"   }
    subscribe -> $, "ble", $                    { pass "*.ble.*"     }
    subscribe -> "bla", :$queue                 { pass "bla"         }
    subscribe -> "bla", :$max-messages          { pass "bla"         }
    subscribe -> "bla", :$queue, :$max-messages { pass "bla"         }
}
is $r2.subscriptions.elems, 11, 'subscriptions block has created 11 subscriptions';

my Supplier $supplier .= new;
my $nats = mocked Nats, returning => {
    subscribe => mocked Nats::Subscription, returning => {
        supply => $supplier.Supply,
    }
}
for $r2.subscriptions -> &subscriptions {
    subscriptions $nats
}
check-mock $nats,
    *.called("subscribe", with => :("bla")),
    *.called("subscribe", with => :("bla.ble")),
    *.called("subscribe", with => :("bla.ble.bli")),
    *.called("subscribe", with => :("*")),
    *.called("subscribe", with => :("*.*")),
    *.called("subscribe", with => :("*.*.*")),
    *.called("subscribe", with => :("bla.*.bli")),
    *.called("subscribe", with => :("*.ble.*")),
    *.called("subscribe", with => :("bla", :$queue)),
    *.called("subscribe", with => :("bla", :$max-messages)),
    *.called("subscribe", with => :("bla", :$queue, :$max-messages)),
;

my @returns = <bla bla.ble bla.ble.bli bla bla.ble bla.ble.bli bla.ble.bli bla.ble.bli bla bla bla>;
$supplier.emit: my $message = mocked Nats::Message, computing => {
    subject => -> { @returns.shift },
}
is @returns.elems, 0, 'supplier has emitted all messages';

check-mock $message, *.called("subject", :11times);

done-testing;
